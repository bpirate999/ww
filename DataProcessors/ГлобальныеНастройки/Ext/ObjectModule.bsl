//ПОДГОТОВКА РЕГИСТРАЦИИ ОБРАБОТКИ

Функция СведенияОВнешнейОбработке() Экспорт
  
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка"); //Варианты: "ДополнительнаяОбработка", "ДополнительныйОтчет", "ЗаполнениеОбъекта", "Отчет", "ПечатнаяФорма", "СозданиеСвязанныхОбъектов" 

	ПараметрыРегистрации.Вставить("Наименование", "Глобальные настройки (Мечел)");
	ПараметрыРегистрации.Вставить("Версия", "1.0"); //"1.0"
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь); //Варианты: Истина, Ложь
	ПараметрыРегистрации.Вставить("Информация", "Обработка предназначена для выполнения глобальных настроек");
	ПараметрыРегистрации.Вставить("ВерсияБСП", "1.2.1.4");// не ниже какой версии БСП подерживается обработка

	ТаблицаКоманд = ПолучитьТаблицуКоманд();

	ДобавитьКоманду(ТаблицаКоманд,
		"Глобальные настройки (Мечел)",
		"Мечел_ГлобальныеНастройки",
		"ОткрытиеФормы",  //Использование.  Варианты: "ОткрытиеФормы", "ВызовКлиентскогоМетода", "ВызовСерверногоМетода"   
		Ложь,//Показывать оповещение. Варианты Истина, Ложь 
		"");//Модификатор 

	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);

	Возврат ПараметрыРегистрации;

КонецФункции

Функция ПолучитьТаблицуКоманд()

	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));

	Возврат Команды;

КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")

	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;

КонецПроцедуры

// {{ ЗУП-00000416 – Хатеев Игорь Валерьевич – 17.04.2019 09:47:48 – Доб.

&НаСервере
Процедура ЗаполнитьСписок(СписокСсылок, ИдентификаторНастройки) Экспорт 
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	#ИмяТаблицы.Ссылка КАК Ссылка,
		|	#ИмяТаблицы.Наименование КАК Наименование
		|ИЗ
		|	#МетаКласс КАК #ИмяТаблицы
		|ГДЕ
		|	#Отбор
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
	СписокСсылок.Очистить();
	
	ИменаТаблиц = ИменаТаблицДляЗапросаПоИдентификатору(ИдентификаторНастройки);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#МетаКласс", ИменаТаблиц.МетаКласс + "." + ИменаТаблиц.ИмяТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицы", ИменаТаблиц.ИмяТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Отбор", ПолучитьОтбор(ИменаТаблиц.ИмяТаблицы, ИдентификаторНастройки));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Обход  = Запрос.Выполнить().Выбрать();
	Пока Обход.Следующий() Цикл
		СписокСсылок.Добавить(Обход.Ссылка, Обход.Наименование);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОтбор(ИмяТаблицы, ИдентификаторНастройки)
	
	Отбор = "ИСТИНА" + Символы.ПС;
	
	Если ИмяТаблицы = "Начисления" Тогда 
		Отбор = Отбор + "И Начисления.ВАрхиве = ЛОЖЬ" + Символы.ПС;
	ИначеЕсли ИмяТаблицы = "ВидыИспользованияРабочегоВремени" ИЛИ ИмяТаблицы = "ВидыВремениНеУчитываеммыеПриСуммированномУчете" Тогда
		Отбор = Отбор + "И ВидыИспользованияРабочегоВремени.НеИспользуется = ЛОЖЬ" + Символы.ПС;
	КонецЕсли;
	
	Если ИдентификаторНастройки = "Вид расчета для начисления аванса сдельщикам" Тогда 
		Отбор = Отбор + 
		" И Начисления.НачисляетсяПриРасчетеПервойПоловиныМесяца
		| И Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПовременнаяОплатаТруда)";
	КонецЕсли;
	
	Возврат Отбор;
	
КонецФункции

&НаСервере
Функция ИменаТаблицДляЗапросаПоИдентификатору(ИдентификаторНастройки)
	
	СоответветствиеИдКоманд = Новый Соответствие;
	СоответветствиеИдКоманд.Вставить("Виды времени не включ. в доплату за праздничные дни", Новый Структура("МетаКласс, ИмяТаблицы", "Справочник", "ВидыИспользованияРабочегоВремени"));
	СоответветствиеИдКоманд.Вставить("Вид расчета для начисления аванса сдельщикам",        Новый Структура("МетаКласс, ИмяТаблицы", "ПланВидовРасчета", "Начисления"));
	СоответветствиеИдКоманд.Вставить("Базовые виды расчета МРОТ",                           Новый Структура("МетаКласс, ИмяТаблицы", "ПланВидовРасчета", "Начисления"));
	СоответветствиеИдКоманд.Вставить("Настройка ВР для расчета ДКВ",                        Новый Структура("МетаКласс, ИмяТаблицы", "ПланВидовРасчета", "Начисления"));
	// {{ ЗУП-00000379 – Хатеев Игорь Валерьевич – 01.02.2019 15:41:38 – Доб.
	СоответветствиеИдКоманд.Вставить("Настройка ВР загружаемых из dbf в НЗВ",               Новый Структура("МетаКласс, ИмяТаблицы", "ПланВидовРасчета", "Начисления"));
	// }} ЗУП-00000379 – Хатеев Игорь Валерьевич – 01.02.2019 15:41:38 – Доб.
	// {{ ЗУП-00000416 – Хатеев Игорь Валерьевич – 18.02.2019 09:01:37 – Доб.
	СоответветствиеИдКоманд.Вставить("Настройка ВР для расчета аванса",                     Новый Структура("МетаКласс, ИмяТаблицы", "ПланВидовРасчета", "Начисления"));
	// }} ЗУП-00000416 – Хатеев Игорь Валерьевич – 18.02.2019 09:01:37 – Доб.
	// {{ ЗУП-00000176 - Сатин Евгений - 11.03.19 - Доб.
	СоответветствиеИдКоманд.Вставить("ВидыВремениНеУчитываеммыеПриСуммированномУчете",      Новый Структура("МетаКласс, ИмяТаблицы", "Справочник", "ВидыИспользованияРабочегоВремени"));
	// }} ЗУП-00000176 - Сатин Евгений - 11.03.19 - Доб.
		// {{ ЗУП.6287 – Бобренкова Ксения Сергеевна – 21.11.2019 10:44:14 – доб.
	СоответветствиеИдКоманд.Вставить("НАСТРОЙКА: Виды времени сотрудников, НЕ учитываемые при расчете доплаты за выслугу лет",     
		Новый Структура("МетаКласс, ИмяТаблицы", "Справочник", "ВидыИспользованияРабочегоВремени"));
	// }} ЗУП.6287 – Бобренкова Ксения Сергеевна – 21.11.2019 10:44:25 – доб.
    // {{ ЗУП.12235 – Бобренкова Ксения Сергеевна – 31.03.2020 11:22:54 – доб.
	СоответветствиеИдКоманд.Вставить("Виды расчета, не учитываемые при расчете среднего заработка", 
		Новый Структура("МетаКласс, ИмяТаблицы", "ПланВидовРасчета", "Начисления"));
	// }} ЗУП.12235 – Бобренкова Ксения Сергеевна – 31.03.2020 11:23:04 – доб.
	
	Возврат СоответветствиеИдКоманд.Получить(ИдентификаторНастройки);
	
КонецФункции

// }} ЗУП-00000416 – Хатеев Игорь Валерьевич – 17.04.2019 09:47:48 – Доб.